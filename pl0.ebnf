program = { block } "." ;

block = [ "const" ident "=" number {"," ident "=" number} ";"]
        [ "var" ident {"," ident} ";"]
        { "procedure" ident ";" block ";" } statement ;

statement = [ scope
              | "?" ident
              | "!" expression
              | "if" condition "then" statement 
              | "call" ident 
              | "while" condition "do" statement
              | assignment_or_expr ];
scope = "begin" statement {";" statement } [";"] "end" ;
assignment_or_expr = [ident ":="] expression ;

condition      = "odd" expression | equality;
equality       = expression ("=="|"!="|"<"|"<="|">"|">=") expression;
expression     = [ "+"|"-"] factor { ("+"|"-") factor};
factor         = primary {("*"|"/") primary};
primary        = ident | number | "(" expression ")";
